/*
 * Copyright Â© Magento, Inc. All rights reserved.
 * See COPYING.txt for license details.
 */

plugins {
    id 'org.jetbrains.intellij' version '0.4.15'
    id 'checkstyle'
    id 'pmd'
    id "com.github.spotbugs" version "3.0.0"
}

repositories {
    mavenCentral()
}

group 'com.magento.idea'
version '1.0.0'

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'groovy'

def phpPluginVersion = System.getProperty("phpPluginVersion", "193.6494.35")
def ideaVersion = System.getProperty("ideaVersion", "2019.3.3")
def javaVersion = 1.8

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

intellij {
    version ideaVersion
    type 'IU'
    pluginName 'com.magento.idea.magento2plugin'
    plugins = ["com.jetbrains.php:$phpPluginVersion", 'yaml', 'java-i18n', 'properties', 'CSS', 'JavaScriptLanguage', 'com.intellij.lang.jsgraphql:2.3.0']
    updateSinceUntilBuild false
    sameSinceUntilBuild false
    downloadSources !Boolean.valueOf(System.getenv('CI'))
    sandboxDirectory "${project.rootDir}/.idea-sandbox"
}

sourceSets {
    main {
        java.srcDirs 'src'
        resources.srcDir 'resources'
    }
    test {
        java.srcDir 'tests'
        resources.srcDir 'testData'
    }
}

/**
 * Static Checks
 */

checkstyle {
    toolVersion = "8.31"
    ignoreFailures = false
}

pmd {
    toolVersion = "6.21.0"
    consoleOutput = true
    rulePriority = 5
    ruleSets = [
            "category/java/codestyle.xml",
            "category/java/design.xml",
            "category/java/errorprone.xml",
            "category/java/bestpractices.xml",
            "category/java/performance.xml",
            "category/java/security.xml",
    ]
}

spotbugs {
    ignoreFailures = false
    showProgress = true
    effort = 'default'
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
    }
}
